programa P1
procesos
  proceso sendIDs
  comenzar
    EnviarMensaje(1,robot1)
    EnviarMensaje(2,robot2)
    EnviarMensaje(3,robot3)
  fin
  proceso LimpiarEsquina (E AvBase:numero ; E CaBase:numero)
  variables
    AvToClean,CaToClean:numero
  comenzar
    RecibirMensaje(AvToClean,fiscal)
    RecibirMensaje(CaToClean,fiscal)
    BloquearEsquina(AvToClean,CaToClean)
    Pos(AvToClean,CaToClean)
    mientras (HayFlorEnLaEsquina)
      tomarFlor
    Pos(AvBase,CaBase)
    LiberarEsquina(AvToClean,CaToClean)
  fin
  proceso pollingID (ES AUX:boolean)
  variables
    ID:numero
    Av,Ca:numero
  comenzar
    RecibirMensaje(ID,*)
    Random(Av,10,30)
    Random(Ca,10,30)
    si (ID=1)
      RecibirMensaje(AUX,robot1)
      si (AUX=V)
        EnviarMensaje(Av,robot1)
        EnviarMensaje(Ca,robot1)
    sino
      si (ID=2)
        RecibirMensaje(AUX,robot2)
        si (AUX=V)
          EnviarMensaje(Av,robot2)
          EnviarMensaje(Ca,robot2)
      sino
        RecibirMensaje(AUX,robot3)
        si (AUX=V)
          EnviarMensaje(Av,robot3)
          EnviarMensaje(Ca,robot3)
  fin
areas
  cuadrante:AreaPC(10,10,30,30)
  punto1:AreaP(1,1,1,1)
  punto2:AreaP(2,1,2,1)
  punto3:AreaP(3,1,3,1)
  puntoF:AreaP(9,9,9,9)
robots
  robot fiscalizador
  variables
    finished:numero
    AUX:boolean
    attTotal:numero
  comenzar
    sendIDs
    finished:=3
    attTotal:=0
    mientras (finished>0)
      pollingID (AUX)
      si (AUX=F)
        finished:=finished-1
      sino
        attTotal:=attTotal+1
    Informar('Los_intentos_totales_realizados_fueron_',attTotal)
  fin
  
  robot florero
  variables
    att:numero
    AvBase,CaBase:numero
    AvToClean,CaToClean:numero
    ID:numero
  comenzar
    AvBase:=PosAv
    CaBase:=PosCa
    RecibirMensaje(ID,fiscal)
    Random(att,2,5) {Para testear, reemplazar por att:=Integer}
    repetir (att)
      EnviarMensaje(ID,fiscal)
      EnviarMensaje(V,fiscal)
      LimpiarEsquina(AvBase,CaBase)
    EnviarMensaje(ID,fiscal)
    EnviarMensaje(F,fiscal)
  fin
variables
  robot1:florero
  robot2:florero
  robot3:florero
  fiscal:fiscalizador
comenzar
  AsignarArea(robot1,cuadrante)
  AsignarArea(robot2,cuadrante)
  AsignarArea(robot3,cuadrante)
  AsignarArea(robot1,punto1)
  AsignarArea(robot2,punto2)
  AsignarArea(robot3,punto3)
  AsignarArea(fiscal,puntoF)
  Iniciar(robot1,1,1)
  Iniciar(robot2,2,1)
  Iniciar(robot3,3,1)
  Iniciar(fiscal,9,9)
fin